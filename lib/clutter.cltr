(def-var! ((vau env () env)) global ((vau env () env)))

(def-var! global %def!-expanders (list))

(def-var! global do
    (vau env (&rest body)
         (eval (list (list* vau #ignore () body)) env)))

(def-var! global cond
    (vau env (&rest forms)
         (if (eql? forms ())
             #f
             (if (eval (car (car forms)) env)
                 (eval (list* do (cdr (car forms)))
                       env)
                 (eval (list* cond (cdr forms)) env)))))

(def-var! global lambda
    (vau env (vau-list &rest body)
         (wrap (eval (list* vau #ignore vau-list body) env))))

(def-var! global assoc
    (lambda (key alist)
      (if (eql? alist (list))
          #f
          (if (eql? key (car (car alist)))
              (car alist)
              (assoc key (cdr alist))))))

(def-var! global mapcar
    (lambda (function values)
      (if (eql? (list) values)
          (list)
          (cons (function (car values)) (mapcar function (cdr values))))))

(def-var! global map
             (lambda (function values)
               (unless (eql? (list) values)
                 (function (car values))
                 (map function (cdr values)))))

(def-var! global let
    (vau env (clauses &rest body)
         (eval (list* (list* lambda (mapcar car clauses) body)
                      (mapcar (lambda (x) (car (cdr x))) clauses))
               env)))

(def-var! global def!
    (vau env (place value)
         (cond
           ((symbol? place)
            (eval (list def-var! env place value) env))
           ((cons? place)
            (let ((expander (assoc (car place) %def!-expanders)))
              (eval (list* (cdr expander) (eval value env) (cdr place))
                    env))))))

(def! %set!-specials (list))

(def! setters-name ((vau env (x) x) .setters))
(eval (list def! setters-name (list make-env)) ((vau env () env)))

(def! set!
    (vau env (variable value)
         (cond
           ((symbol? variable)
            (let ((special (assoc variable %set!-specials)))
              (if special
                  ((cdr special) (eval value env))
                  (eval (list set-var! env variable value) env))))
           ((cons? variable)
            (let ((expander (eval (list lookup (car variable) setters-name) env)))
              (eval (list* expander (eval value env) (cdr variable)) env))))))

(def! push!
    (vau env (value place)
         (eval (list set! place (list cons value place))
               env)))

(def! def-def!-expander!
    (vau env (name combiner)
         (push! (cons name (eval combiner env)) %def!-expanders)))

(def-def!-expander! lookup
    (vau env (value symbol target-env)
         (eval (list def-lookup! (eval value env) symbol (eval target-env env))
               env)))

(def! get-current-env
  (wrap (vau env () env)))

(def! rcurry
    (lambda (function &rest last-args)
      (lambda (&rest first-args)
        (apply function (append first-args last-args)))))

(def! with-env
    (vau env (module &rest body)
         (map (rcurry eval (eval module env))
              body)))

(def! apply
  (lambda (function args)
    (eval (cons (unwrap function) args) (get-current-env))))

(def! when
    (vau env (test &rest body)
         (if (eval test env)
             (eval (list* do body) env)
             #f)))

(def! unless
    (vau env (test &rest body)
         (if (eval test env)
             #f
             (eval (list* do body) env))))

(def! def-set!-special!
    (vau env (variable function)
         (push! (cons variable (eval function env)) %set!-specials)))

(def! def-set!-expander!
    (vau env (name combiner)
         (unless (bound? setters-name env)
           (eval (list def! setters-name (make-env (env-parent env))) env))
         (eval (list def-lookup! combiner name setters-name) env)))

(def-set!-expander! lookup
    (vau env (value symbol target-env)
         (eval (list set-lookup! (eval value env) symbol (eval target-env env))
               env)))

(def! anaphoric
    (vau env (var test op &rest body)
         (eval (list let (list (list var (eval test env)))
                     (list* op var body))
               env)))

(def! aif
    (vau env (var test then else)
         (eval (list anaphoric var test if then else) env)))

(def! awhen
    (vau env (var test &rest body)
         (eval (list* anaphoric var test when body) env)))

(def! aunless
    (vau env (var test &rest body)
         (eval (list* anaphoric var test unless body) env)))
