(direct-def! %def!-expanders (list))

(direct-def! do
    (vau env (&rest body)
         (eval (list (list* vau #ignore () body)) env)))

(direct-def! cond
    (vau env (&rest forms)
         (if (eql? forms ())
             #f
             (if (eval (car (car forms)) env)
                 (eval (list* do (cdr (car forms)))
                       env)
                 (eval (list* cond (cdr forms)) env)))))

(direct-def! lambda
    (vau env (vau-list &rest body)
         (wrap (eval (list* vau #ignore vau-list body) env))))

(direct-def! assoc
    (lambda (key alist)
      (if (eql? alist (list))
          #f
          (if (eql? key (car (car alist)))
              (car alist)
              (assoc key (cdr alist))))))

(direct-def! mapcar
    (lambda (function values)
      (if (eql? (list) values)
          (list)
          (cons (function (car values)) (mapcar function (cdr values))))))

(direct-def! let
    (vau env (clauses &rest body)
         (eval (list* (list* lambda (mapcar car clauses) body)
                      (mapcar (lambda (x) (car (cdr x))) clauses))
               env)))

(direct-def! def!
    (vau env (place value)
         (cond
           ((symbol? place)
            (eval (list direct-def! place value) env))
           ((cons? place)
            (let ((expander (assoc (car place) %def!-expanders)))
              (eval (list* (cdr expander) (eval value env) (cdr place))
                    env))))))

(def! %set!-specials (list))

(def! setters-name ((vau env (x) x) .setters))
(eval (list def! setters-name (list make-env)) ((vau env () env)))

(def! set!
    (vau env (variable value)
         (cond
           ((symbol? variable)
            (let ((special (assoc variable %set!-specials)))
              (if special
                  ((cdr special) (eval value env))
                  (eval (list direct-set! variable value) env))))
           ((cons? variable)
            (let ((expander (eval (list lookup (car variable) setters-name) env)))
              (eval (list* expander (eval value env) (cdr variable)) env))))))

(def! push!
    (vau env (value place)
         (eval (list set! place (list cons value place))
               env)))

(def! def-def!-expander!
    (vau env (name combiner)
         (push! (cons name (eval combiner env)) %def!-expanders)))

(def-def!-expander! lookup
    (vau env (value symbol target-env)
         (eval (list def-lookup! (eval value env) symbol (eval target-env env))
               env)))

(def! get-current-env
  (wrap (vau env () env)))

(def! def-ns!
    (vau env (name)
         (let ((new-env (make-env env)))
           ;; FIXME: Parent skips intermediary namespaces
           (eval (list def-lookup! (list make-env (list eval setters-name env)) setters-name new-env) env)
           (eval (list def! name new-env) env))))

(def! with-ns
    (vau env (ns &rest body)
         (eval (list (list* lambda () body)) (eval ns env))))

(def! apply
  (lambda (function args)
    (eval (cons (unwrap function) args) (get-current-env))))

(def! def-set!-special!
    (vau env (variable function)
         (push! (cons variable (eval function env)) %set!-specials)))

(def! def-set!-expander!
    (vau env (name combiner)
         (eval (list def-lookup! combiner name setters-name) env)))

(def-set!-expander! lookup
    (vau env (value symbol target-env)
         (eval (list set-lookup! (eval value env) symbol (eval target-env env))
               env)))

(def! when
    (vau env (test &rest body)
         (if (eval test env)
             (eval (list* do body) env)
             #f)))

(def! unless
    (vau env (test &rest body)
         (if (eval test env)
             #f
             (eval (list* do body) env))))

(def! anaphoric
    (vau env (var test op &rest body)
         (eval (list let (list (list var (eval test env)))
                     (list* op var body))
               env)))

(def! aif
    (vau env (var test then else)
         (eval (list anaphoric var test if then else) env)))

(def! awhen
    (vau env (var test &rest body)
         (eval (list* anaphoric var test when body) env)))

(def! aunless
    (vau env (var test &rest body)
         (eval (list* anaphoric var test unless body) env)))
